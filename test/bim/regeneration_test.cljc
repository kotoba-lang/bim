(ns bim.regeneration-test
  (:require [bim :as bim]
            [bim.regeneration :as regeneration]
            [clojure.test :refer [deftest is]]
            [clojure.string :as string]
            [kotoba.document.artifact-graph :as artifact-graph]))

(defn- project-with-wall [length]
  (let [wall (bim/wall {:id 100 :start [0 0 0] :end [length 0 0] :height 3.0})
        storey (bim/storey {:id 10 :name "Ground" :elevation 0 :height 3
                            :placement :identity :spaces [] :elements [wall]})]
    (assoc (bim/project "Tower") :id "tower"
           :sites [(bim/site {:id 1 :name "Site" :placement :identity
                              :reference-location [0 0 0]
                              :buildings [(bim/building
                                           {:id 2 :name "Building"
                                            :placement :identity
                                            :reference-elevation 0
                                            :storeys [storey]})]})])))

(deftest regenerates-all-coordinated-artifacts-and-reuses-unchanged-results
  (let [first-run (regeneration/regenerate {:project (project-with-wall 6)})
        reused (regeneration/regenerate {:project (project-with-wall 6)} first-run)
        changed (regeneration/regenerate {:project (project-with-wall 9)} reused)]
    (is (= #{:structural/model :structural/results :mep/designs :coordinated/project
             :drawing/set :ifc/document :ifc/spf}
           (:artifact.graph/rebuilt first-run)))
    (is (empty? (:artifact.graph/rebuilt reused)))
    (is (= 1 (get-in first-run [:design/drawing-set :drawing/generation])))
    (is (string/includes? (:design/ifc-spf first-run) "IFCPROJECT"))
    (is (= 9.0 (get-in changed [:design/project :sites 0 :buildings 0 :storeys 0
                                :elements 0 :quantities :length-m])))
    (is (contains? (:artifact.graph/rebuilt changed) :ifc/spf))
    (is (= 2 (get-in changed [:design/drawing-set :drawing/generation])))))

(deftest can-target-ifc-without-building-drawings
  (let [result (regeneration/regenerate
                {:project (project-with-wall 4)}
                (artifact-graph/state)
                {:targets [:ifc/spf]})]
    (is (string? (:design/ifc-spf result)))
    (is (nil? (:design/drawing-set result)))
    (is (not (contains? (:artifact.graph/rebuilt result) :drawing/set)))))
