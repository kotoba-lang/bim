(ns bim.dwg-test
  (:require [bim :as bim]
            [bim.interchange.dwg :as dwg]
            [clojure.test :refer [deftest is]])
  (:import [java.nio.charset StandardCharsets]
           [java.nio.file Files StandardOpenOption]
           [java.nio.file.attribute FileAttribute]))

(def storey
  (bim/storey {:id 1 :name "Ground" :elevation 0 :height 3 :placement :identity
               :spaces [] :elements [(bim/wall {:id 1 :name "Wall"
                                                 :start [0 0 0] :end [3 0 0]
                                                 :height 3})]}))

(deftest invokes-converter-and-verifies-real-dwg-signature
  (let [script (Files/createTempFile "kotoba-dwg-converter-" ".sh"
                                     (make-array FileAttribute 0))
        output (Files/createTempFile "kotoba-dwg-output-" ".dwg"
                                     (make-array FileAttribute 0))]
    (try
      (Files/writeString script "#!/bin/sh\nprintf 'AC1032converted' > \"$2\"\n"
                         StandardCharsets/UTF_8
                         (into-array StandardOpenOption [StandardOpenOption/WRITE
                                                         StandardOpenOption/TRUNCATE_EXISTING]))
      (.setExecutable (.toFile script) true)
      (let [result (dwg/export-floor-plan! storey output
                                            {:command [(str script) "{input}" "{output}"]})]
        (is (= "AC1032" (:dwg/signature result)))
        (is (< 6 (:dwg/size result))))
      (finally
        (Files/deleteIfExists script)
        (Files/deleteIfExists output)))))

(deftest rejects-a-converter-that-only-renames-dxf
  (let [output (Files/createTempFile "kotoba-not-dwg-" ".dwg"
                                     (make-array FileAttribute 0))]
    (try
      (is (thrown-with-msg? Exception #"not a recognized DWG"
                            (dwg/export-floor-plan!
                             storey output {:command ["cp" "{input}" "{output}"]})))
      (finally (Files/deleteIfExists output)))))
